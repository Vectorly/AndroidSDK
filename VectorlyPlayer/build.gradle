import com.android.builder.core.BuilderConstants

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'


// update these next lines to fit your submodule
group = GROUP_ID
version = VERSION_CODE

// Add sources as an artifact
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

// Loop over all variants
android.libraryVariants.all { variant ->

    def name = variant.buildType.name
    if (name == BuilderConstants.DEBUG) {
        return // Skip debug builds.
    }

    variant.outputs.all { output ->
        // This creates a publication for each variant
        publishing.publications.create(variant.name, MavenPublication) {
            // The sources artifact from earlier
            artifact sourceJar

            // Variant dependent artifact, e.g. release, debug
            artifact source: output.outputFile, classifier: output.name

            // Go through all the dependencies for each variant and add them to the POM
            // file as dependencies
            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')

                // Filter out anything that's not an external dependency. You shouldn't
                // be publishing artifacts that depend on local (e.g. project) dependencies,
                // but who knows...
                configurations.getByName(variant.name + "CompileClasspath").allDependencies
                        .findAll { it instanceof ExternalDependency }
                        .each {
                    def dependency = dependencies.appendNode('dependency')

                    dependency.appendNode('groupId', it.group)
                    dependency.appendNode('artifactId', it.name)
                    dependency.appendNode('version', it.version)
                }
            }
        }
    }
}

// Ensure that the publish task depends on assembly
tasks.all { task ->
    if (task instanceof AbstractPublishToMaven) {
        task.dependsOn assemble
    }
}

// Configure the destination repository with
// S3 URL and access credentials
publishing {
    repositories {
        maven {
            url AWS_S3_RELEASE_BUCKET
            credentials(AwsCredentials) {
                accessKey AWS_S3_ACCESS_KEY
                secretKey AWS_S3_SECRET_KEY
            }
        }
    }
}

android {

    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    bintrayRepo = 'VectorlyPlayer' // your repo name
    bintrayName = 'io.vectorly.player' // has to be same as your library module name
    publishedGroupId = 'io.vectorly.player' // your module package name
    libraryName = 'io.vectorly.player'
    artifact = 'io.vectorly.player' // has to be same as your library module name
    libraryDescription = 'VectorlyPlayer is a simple View that you can plugin to your Android apps to quickly get vectorized video playback working.'
    siteUrl = 'https://github.com/Vectorly/AndroidSDK'
    gitUrl = 'https://github.com/Vectorly/AndroidSDK.git'
    libraryVersion = '1.0.0'
    developerId = 'shawinder'
    developerName = 'Shawinder'
    developerEmail = 'shawinder.impinge@gmail.com'
    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        userOrg = 'vectorly'
        repo = "VectorlyPlayer"
        name = "VectorlyPlayer"
        websiteUrl = "https://github.com/Vectorly/AndroidSDK"
        vcsUrl = "https://github.com/Vectorly/AndroidSDK.git"
        licenses = ["Apache-2.0"]
        publish = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    api 'com.android.support:appcompat-v7:27.1.1'

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation "com.squareup.okhttp3:logging-interceptor:3.8.1"
    api 'io.paperdb:paperdb:2.6'

}

if (project.rootProject.file('local.properties').exists()) {
    apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
    apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
}

/*tasks.withType(Javadoc) {
    options.addBooleanOption('Xdoclint:none', true)
}*/

tasks.withType(Javadoc).all {
    enabled = false
}

//task javadoc(type: Javadoc) {
//    excludes = ['**/*.kt'] // < ---- Exclude all kotlin files from javadoc file.
//}